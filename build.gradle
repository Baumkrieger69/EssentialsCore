plugins {
    id 'java'
    id('xyz.jpenilla.run-paper') version "2.3.1"
    id 'maven-publish'
}

group = 'com.github.Baumkrieger69'
version = '1.0.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT")
}

// API SourceSet definieren
sourceSets {
    api {
        java {
            srcDir 'src/main/java'
            include 'com/essentialscore/api/**'
        }
    }
}

tasks {
    runServer {
        minecraftVersion("1.21")
    }

    // Haupt-JAR
    jar {
        archiveBaseName.set(project.name)
        archiveClassifier.set('')
        archiveVersion.set(project.version.toString())
        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }

    // API-JAR
    task apiJar(type: Jar) {
        from sourceSets.api.output
        archiveBaseName.set("${project.name}-api")
        archiveClassifier.set('api')
        archiveVersion.set(project.version.toString())
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// API als separates Artefakt hinzuf√ºgen
artifacts {
    api tasks.named('apiJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.named('apiJar')
            
            pom {
                name = 'EssentialsCore'
                description = 'Minecraft Module structure Plugin'
                url = 'https://github.com/Baumkrieger69/EssentialsCore'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Baumkrieger69'
                        name = 'Baumkrieger69'
                    }
                }
            }
        }
    }
}
